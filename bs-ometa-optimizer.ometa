// TODO: turn off the "seq" inliner when G.seq !== OMeta.seq (will require some refactoring)
// TODO: add a factorizing optimization (will make jumptables more useful)

let BSNullOptimization = ometa {
  setHelped = !(this._didSomething = true),
  helped    = ?this._didSomething,
  trans     = [:t ?(this[t] != undefined) apply(t):ans] -> ans,
  optimize  = trans:x helped                         -> x,

  App        :rule anything*:args          -> ['App', rule].concat(args),
  Pred       :expr                         -> ['Pred', expr],
  Or         trans*:xs                     -> ['Or'].concat(xs),
  XOr        trans*:xs                     -> ['XOr'].concat(xs),
  And        trans*:xs                     -> ['And'].concat(xs),
  Opt        trans:x                       -> ['Opt',  x],
  Many       trans:x                       -> ['Many',  x],
  Many1      trans:x                       -> ['Many1', x],
  Set        :n trans:v                    -> ['Set', n, v],
  Not        trans:x                       -> ['Not',       x],
  Lookahead  trans:x                       -> ['Lookahead', x],
  Form       trans:x                       -> ['Form',      x],
  ConsBy     trans:x                       -> ['ConsBy',    x],
  IdxConsBy  trans:x                       -> ['IdxConsBy', x],
  JumpTable  ([:c trans:e] -> [c, e])*:ces -> ['JumpTable'].concat(ces),
  Interleave ([:m trans:p] -> [m, p])*:xs  -> ['Interleave'].concat(xs),

  Js         :code                         -> ['Js', code],
  Location                                 -> ['Location'],

  Rule       :name :location trans:body    -> ['Rule', name, location, body]
};
BSNullOptimization.initialize = function() { this._didSomething = false; };

let BSAssociativeOptimization = ometa(BSNullOptimization) {
  And trans:x end           setHelped -> x,
  And transInside('And'):xs           -> ['And'].concat(xs),
  Or  trans:x end           setHelped -> x,
  Or  transInside('Or'):xs            -> ['Or'].concat(xs),
  XOr trans:x end           setHelped -> x,
  XOr transInside('XOr'):xs           -> ['XOr'].concat(xs),

  transInside :t = [exactly(t) transInside(t):xs] transInside(t):ys setHelped -> xs.concat(ys)
                 | trans:x                        transInside(t):xs           -> [x].concat(xs)
                 |                                                            -> []
};

let BSSeqInliner = ometa(BSNullOptimization) {
  App        = 'seq'
               ['Js' ['string' :s]] end
               seqString(s):cs setHelped              -> ['And'].concat(cs).concat([jsValue('string', s)])
             | :rule anything*:args                   -> ['App', rule].concat(args),
  inlineChar = BSOMetaParser.eChar:c                  -> ['App', 'exactly', jsValue('string', c)],
  seqString = &(:s ?(typeof s === 'string'))
              [ inlineChar*:cs ]                      -> cs

};

let JumpTable = function(choiceOp, choice) {
  this.choiceOp = choiceOp;
  this.choices = {};
  this.add(choice);
};
JumpTable.prototype.add = function(choice) {
  var c = choice[0], t = choice[1];
  if (this.choices[c]) {
    if (this.choices[c][0] == this.choiceOp)
      this.choices[c].push(t);
    else
      this.choices[c] = [this.choiceOp, this.choices[c], t];
  }
  else
    this.choices[c] = t;
};
JumpTable.prototype.toTree = function() {
  var r = ['JumpTable'], choiceKeys = ownPropertyNames(this.choices);
  for (var i = 0; i < choiceKeys.length; i += 1)
    r.push([choiceKeys[i], this.choices[choiceKeys[i]]]);
  return r;
};
let BSJumpTableOptimization = ometa(BSNullOptimization) {
  Or  (jtChoices('Or')  | trans)*:cs -> ['Or'].concat(cs),
  XOr (jtChoices('XOr') | trans)*:cs -> ['XOr'].concat(cs),
  quotedString  = ['Js' ['string' :s ]] -> s,
  jtChoice      = ['And' ['App' 'exactly' quotedString:x] anything*:rest]                  -> [x, ['And'].concat(rest)]
                |        ['App' 'exactly' quotedString:x]                                  -> [x, jsValue('string', x)],
  jtChoices :op = jtChoice:c {new JumpTable(op, c)}:jt (jtChoice:c {jt.add(c)})* setHelped -> jt.toTree()
};

let BSFunctionOptimization = ometa(GenericMatcher) {
  optimize  = trans:x -> x,

  trans = [:t ?(this[t] != undefined) apply(t):ans] -> ans,

  somethingThatReturns = [ exactly(#And) apply(#AndReturn):ans ] -> ans
                       | [ exactly(#And) end ]                   -> ['ReturnStructure', ['And']]
                       | [ generatesStructure:t apply(t):ans ]   -> ['ReturnStructure', ['Store', ans]]
                       | [ exactly(#Set) apply(#Set):ans ]       -> ['ReturnStructure', ['Parenthesis', ans]]
                       | trans:x                                 -> ['ReturnStructure', x],

  generatesStructure = #Or | #XOr | #Opt | #Many | #Many1 | #Not
                     | #Lookahead | #Form | #ConsBy | #IdxConsBy
                     | #App | #JumpTable | #Interleave,

  somethingThatCanPass      = [ generatesStructure:t
                                apply(t):ans ]            -> ans,

  storeSomething            = [ generatesStructure:t
                                apply(t):ans ]            -> ['Store', ans]
                            | trans:x,
  storeSomethingThatReturns = somethingThatReturns:x,

  functioned = somethingThatCanPass:x                     -> ['Function', ['Return', x]]
             | storeSomethingThatReturns:x                -> ['FunctionStructure', x],

  AndReturn  notLast(#storeSomething)*:xs
             storeSomethingThatReturns:y                  -> ['And'].concat(xs.concat([y])),

  Or         functioned*:xs                               -> ['Or'].concat(xs),
  XOr        functioned*:xs                               -> ['XOr'].concat(xs),
  Opt        functioned:x                                 -> ['Opt',  x],
  Many       functioned:x                                 -> ['Many',  x],
  Many1      functioned:x                                 -> ['Many1', x],
  Not        functioned:x                                 -> ['Not',       x],
  Lookahead  functioned:x                                 -> ['Lookahead', x],
  Form       functioned:x                                 -> ['Form',      x],
  ConsBy     functioned:x                                 -> ['ConsBy',    x],
  IdxConsBy  functioned:x                                 -> ['IdxConsBy', x],
  App        :rule trans*:args                            -> ['App', rule].concat(args),
  JumpTable  ([:c somethingThatReturns:e] -> [c, e])*:ces -> ['JumpTable'].concat(ces),
  Interleave ([:m functioned:p] -> [m, p])*:xs            -> ['Interleave'].concat(xs),

  // Do not generate structure :
  Pred       :expr                                        -> ['Pred', expr],
  Set        :n storeSomething:v                          -> ['Set', n, v],

  Js         :code                                        -> ['Js', code],
  Location                                                -> ['Location'],

  Rule       :name :location
             ( somethingThatCanPass:b
                    -> ['ReturnStructure', ['Store', b]]
             | somethingThatReturns ):body                -> ['Rule', name, location, body]
};

let BSOMetaOptimizer = ometa {
  optimizeGrammar = ['Grammar' :sn optimizeRule*:rs]          -> ['Grammar', sn].concat(rs),
  optimizeRule    = :r ( BSSeqInliner.optimize(r):r
                       | empty )

                       ( BSAssociativeOptimization.optimize(r):r
                       | BSJumpTableOptimization.optimize(r):r )*

                       BSFunctionOptimization.optimize(r):r
    -> r
};
